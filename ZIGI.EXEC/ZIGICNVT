  /* --------------------  rexx procedure  -------------------- *
  | Name:      zigicnvt                                        |
  |                                                            |
  | Function:  ZIGI convert a non-ZIGI repo to ZIGI format     |
  |                                                            |
  | Syntax:    %zigicnvt                                       |
  |                                                            |
  | Usage Notes:                                               |
  |            1. ISPF Variables will pass the                 |
  |               - default HLQ to be used                     |
  |            2. The OMVS Directory where the project is      |
  |            3. Sequential files that have no lowercase      |
  |               will be processed.                           |
  |            4. Directories that are all upper case and      |
  |               8 chars or less will be assumed to be        |
  |               a PDS                                        |
  |            5. Files ending in .bin will have the .bin      |
  |               removed and be defined as binary             |
  |                                                            |
  | Dependencies: Will call zigickot when done to upload       |
  |               to z/OS                                      |
  |                                                            |
  | Author:    Lionel B. Dyck                                  |
  |                                                            |
  | History:  (most recent on top)                             |
  |            02/03/22 LBD - Replace dsnvalid routine         |
  |            10/25/20 LBD - Improved usssafe routine         |
  |            10/20/20 LBD - Add .gitattributes if not there  |
  |            08/08/20 LBD - Generalize get_binfiles          |
  |            03/02/20 LBD - Minor cleanup                    |
  |            02/27/20 LBD - Creation                         |
  | ---------------------------------------------------------- |
  |    ZIGI - the z/OS ISPF Git Interface                      |
  |    Copyright (C) 2020-2023 - Henri Kuiper and Lionel Dyck  |
  |                                                            |
  |    This program is free software: you can redistribute it  |
  |    and/or modify it under the terms of the GNU General     |
  |    Public License as published by the Free Software        |
  |    Foundation, either version 3 of the License, or (at     |
  |    your option) any later version.                         |
  |                                                            |
  |    This program is distributed in the hope that it will be |
  |    useful, but WITHOUT ANY WARRANTY; without even the      |
  |    implied warranty of MERCHANTABILITY or FITNESS FOR A    |
  |    PARTICULAR PURPOSE.  See the GNU General Public License |
  |    for more details.                                       |
  |                                                            |
  |    You should have received a copy of the GNU General      |
  |    Public License along with this program.  If not, see    |
  |    <https://www.gnu.org/licenses/>.                        |
  * ---------------------------------------------------------- */

  address ispexec,
    'vget (convdir)'

  parse value '' with null fail binfiles.

  if convdir = null then exit 8

  address syscall 'lstat' convdir'/.gitattributes g.'
  if g.0 = 0 then call create_gitattributes convdir

  x = syscalls('ON')
  call readdir convdir
  /* process the root directory */
  do i = 1 to d.0
    fileo = d.i
    type = s.i.st_type  /* 3 = ps 1 = dir */
    if left(fileo,1) = '.' then iterate
    if right(fileo,3) = '.md' then iterate
    file = translate(fileo)
    if dsnvalid(file) = 'OK' then call convert
  end

  x = syscalls('OFF')

  call outtrap 'x.'
  call make_dsn convdir
  call outtrap 'off'
  exit 0

convert:
  zs1 = 'Working on conversion'
  zs2 = 'Patience.'
  call pop
  if type = 1 then do
    dir = convdir'/'fileo
    zs1 = 'Reading' dir
    zs2 = 'Patience.'
    call pop
    call readsub dir
    do di = 1 to sd.0
      stype = ss.di.st_type  /* 3 = ps 1 = dir */
      if stype = 1 then iterate
      dfile = sd.di
      if left(dfile,1) /= '.' then iterate
      parse value dfile with ndfile'.'suf
      ndfile = translate(ndfile)
      cmd = 'cd' dir '&& mv' dfile ndfile
      zs1 = 'Converting:' dfile
      zs2 = 'To:        ' ndfile
      call pop
      cmd = usssafe(cmd)
      x = bpxwunix(cmd,,so.,se.)
      if x > 0 then do
        do z = 1 to so.0;say so.z;end
        do z = 1 to se.0;say se.z;end
      end
      if translate(suf) = 'BIN' then do
        call set_binary
      end
    end
    zs1 = 'Renaming Directory:' fileo
    zs2 = 'To                :' file
    call pop
    cmd = 'cd' convdir '&& mv' fileo file
    cmd = usssafe(cmd)
    x = bpxwunix(cmd,,so.,se.)
    if x > 0 then do
      do z = 1 to so.0;say so.z;end
      do z = 1 to se.0;say se.z;end
    end
  end
  if type = 3 then do
    if right(file,4) = '.BIN' then do
      parse value file with file'.BIN'
      call set_binary
    end
    if file /= fileo then do
      zs1 = 'Converting:' fileo
      zs2 = 'To        :' file
      call pop
      cmd = 'cd' convdir '&& mv' fileo file
      cmd = usssafe(cmd)
      x = bpxwunix(cmd,,so.,se.)
      if x > 0 then do
        do z = 1 to so.0;say so.z;end
        do z = 1 to se.0;say se.z;end
      end
    end
  end
  return

/* ---------------------------- *
 | Create a .gitattributes file |
 * ---------------------------- */
Create_GitAttributes: Procedure
  parse arg convdir
  /* The git repo is initialized. Time to stick a
  .gitattributes (ASCII) in there */
  call syscalls 'ON'
  address syscall
  path = convdir"/.ga"
  'open' path,
    O_rdwr+O_creat+O_trunc,
    660
  if retval = -1 then do
    Address ISPExec
    zs1 = "Something bad when creating .gitattributes."
    zs2 = 'Press F3 to close this popup and exit.'
    call pop
    call syscalls 'OFF'
    return
  end
  fd = retval  /* as I think it's weird to write to a retval lol */
  call write_fd ,
    "# This .gitattributes file is autogenerated with ZIGI"
  call write_fd ,
    "*   git-encoding=iso8859-1 zos-working-tree-encoding="encoding
  ga3a = ".gitattributes    "
  ga3b = "git-encoding=iso8859-1 zos-working-tree-encoding=iso8859-1"
  ga3  = ga3a || ga3b
  call write_fd ga3
  ga4a = ".gitignore        "
  ga4b = "git-encoding=IBM-1047 zos-working-tree-encoding=IBM-1047"
  ga4  = ga4a || ga4b
  call write_fd  ga4
  binaries = '*.docm *.docx *.doc *.dotx *.pdf *.epub *.mobi *.azw3' ,
    '*.jpg *.jpeg *.png *.gif *.zip *.gzip *.tz *.z *.Z *.pax' ,
    '*.ppt *.pptx *.xls *.xlsx *.xlsm *.obj *.bin'
  do gai = 1 to words(binaries)
    ga = word(binaries,gai) 'binary'
    call write_fd ga
  end
  'close' fd
  call syscalls 'OFF'

  /* -------------------- .gitattributes written ----------- */
  /*             UNFORTUNATELY, IT'S IN EBCDIC :(            */
  /* ------------------------------------------------------- */
  /* convert to iso8859-1 */
  cmd = 'cd' convdir ' && '
  cmd = cmd '/bin/iconv -f ibm-1047 -t iso8859-1 .ga'
  cmd = cmd ' > .gitattributes'
  x = bpxwunix(cmd)

  /* delete the .ga file */
  cmd = 'cd' convdir ' && rm -rf .ga'
  x = bpxwunix(cmd)

  /* tag it and bag it */
  cmd = 'cd' convdir ' && '
  cmd = cmd 'chtag -tc iso8859-1 .gitattributes'
  x = bpxwunix(cmd)

  return

Write_FD:
  parse arg string
  string = string || ESC_N
  'write' fd 'string' length(string)
  return

  /* ----------------------- *
  | Generalized Pop Message |
  * ----------------------- */
Pop:
  add = address()             /* save addressing environment */
  Address ISPExec
  call do_popup
  Address add                 /* restore addressing environment */
  return

  /* ---------------------------------------- *
  | Common routine for normal (2 line) popup |
  * ---------------------------------------- */
Do_Popup:
  call pfshow 'off'           /* make sure pfshow is off */
  'Control Display Lock'
  'Addpop row(4) column(8)'
  'Display Panel(zigipop)'
  'Rempop'
  call pfshow 'reset'         /* restore pfshow setting */
  return

  /* ------------------------------------------------------ *
  | The pfshow routine will:                               |
  | 1. check to see the passed option                      |
  | 2. if Off then it will save the current pfshow setting |
  |    - save the current setting                          |
  |    - turn off pfshow                                   |
  | 3. if the option is Reset then it will                 |
  |    - test if pfshow was on and turn it back on         |
  * ------------------------------------------------------ */
pfshow:
  if zpfshow = 'OFF' then return
  arg pfkopt
  if pfkopt = 'RESET' then do
    if pfkeys = 'ON' then
    'select pgm(ispopf) parm(FKA,ON)'
  end
  if pfkopt = 'OFF' then do
    'vget (zpfshow)'
    pfkeys = zpfshow
    if pfkeys /= 'OFF' then
    'select pgm(ispopf) parm(FKA,OFF)'
  end
  return

Readsub:
  parse arg dir
  address syscall 'readdir' dir 'sd. ss.'
  return

Readdir:
  parse arg dir
  address syscall 'readdir' dir  'd. s.'
  return

Make_dsn:
  parse arg dir
  x = bpxwunix('cd' dir '&& mkdir .zigi && chmod 755 .zigi')
  r.1 = "# ZIGI dsn-file "
  r.2 = "# This file needs to be here."
  r.3 = "# Don't edit unless you know what you're doing :)"
  r.4 = '#  '
  r.5 = '# record format is (case insensitive):'
  r.6 = '#  '
  r.7 = '# Position - description'
  r.8 = '#  '
  r.9 = '# 1 - directory name which will be prefixed by the'
  r.10 = '#     defined HLQ to create the z/OS dataset'
  r.11 = '#     (* defines the default)'
  r.12 = '# 2 - PS or PO (dataset organization)'
  r.13 = '#     PS for a flat file'
  r.14 = '#     PO for a directory and thus a partitioned dataset'
  r.15 = '# 3 - record format (FB or VB)'
  r.16 = '# 4 - lrecl'
  r.17 = '# 5 - blksize'
  r.18 = '#     0 is allowed if system determined blksize is enabled'
  r.19 = '# 6 - file-extension (optional and no-period)'
  r.20 = '#  '
  r.21 = '# Default DSORG and DCB info'
  r.22 = '* PO FB 80 32720'
  r.0 = 22
  Address TSO
  dsn = "'"dir"/.zigi/dsn'"
  dd = 'cv'random(9999)
  if sysvar('syspref') = null
  then pref = sysvar('sysuid')
  else pref = sysvar('syspref')
  'alloc f('dd') new spa(1,1) tr recfm(f b) lrecl(80) blksize(0)' ,
    "ds('"pref'.'dd"')"
  'Execio * diskw' dd '(finis stem r.'
  'free f('dd')'
  'oput' "'"pref'.'dd"'" dsn 'text'
  'delete' "'"pref"."dd"'"
  return

dsnvalid:
  /* Tests for a 'potentially valid' z/OS datasetname */
  parse arg val_dsn
  if dsninfo.val_dsn /= null then return 'OK'
  if left(val_dsn,1) = '.' then return 'NO'
  if strip(val_dsn) = null then return 'NO'
  if pos(' ',val_dsn) > 0 then return 'NO'
  val_dsn = strip(val_dsn,'B',"'")
  upp = TRANSLATE(val_dsn,".ABCDEFGHIJKLMNOPQRSTUVWXYZ",,
    ".abcdefghijklmnopqrstuvwxyz")
  tdsn = translate(val_dsn,' ','.')
  retval = 'OK'   /* asssume good */
  do dsnvi = 1 to words(tdsn)
    w = word(tdsn,dsnvi)
    if length(w) > 8 then retval = 'NO'
    if pos(left(w,1),'ABCDEFGHIJKLMNOPQRSTUVWXYZ$#@') = 0 then retval = 'NO'
    w = translate(w,copies(' ',40), ,
      'ABCDEFGHIJKLMNOPQRSTUVWXYZ-$#@0123456789')
    w = strip(w)
    if w /= null then retval = 'NO'
  end
  if retval /= 'NO' then
  if upp = val_dsn then do
    smsg = msg()
    call msg 'OFF'
    if sysdsn(val_dsn) = "OK" | sysdsn(val_dsn) = "DATASET NOT FOUND"
    then retval = 'OK'
    else retval = "NO"
    call msg smsg
  end
  return retval

usssafe:
 parse arg safedsn
 if pos('$',safedsn) = 0 then return safedsn
 safe$pos = 1
 do forever
    pos$safe = pos('$',safedsn,safe$pos)
    if pos$safe < 1 then return safedsn
    left$safe = left(safedsn,pos$safe-1)
    right$save = substr(safedsn,pos$safe)
    safedsn = left$safe'\'right$save
    safe$pos = pos$safe + 2
    end
 return safedsn

oldsafe:
  parse arg safedsn
  if pos('$',safedsn) = 0 then return safedsn
  /* Let's not usssafe it twice :) */
  if pos('\$',safedsn) > 0 then return safedsn
  safedsn = strreplace(safedsn, '$', '\$')
  return safedsn

strreplace:
  string  = arg(1)
  strfrom = arg(2)
  strto   = arg(3)
  if pos(strfrom,string) = 0 then return string
  newString = ''
  do repi = 1 to length(string)
    if substr(string,repi,1) /= strfrom
    then newstring = newstring''substr(string,repi,1)
    else  newstring = newstring''strto
  end
  return newstring

Set_Binary:
  if bin_already(ndfile) = 0 then do
    binopt = '-B'
      /* setting the gitattributes file */
      /* first convert the ASCII atrtib file to ebcdic :( */
    gabs = 'git-encoding=BINARY zos-working-tree-encoding=BINARY binary'
    cmd = 'cd' convdir '&& /bin/iconv -f ISO8859-1 -t IBM-1047 '
    cmd = cmd '.gitattributes > .gae'
      /* then append the binary attrib for this file */
    if type = 1 then
    cmd = cmd '&& echo "'file'/'ndfile' 'gabs'" >> .gae '
    if type = 3 then
    cmd = cmd '&& echo "'file gabs'" >> .gae '
      /* convert it back to ascii */
    cmd = cmd '&& /bin/iconv -f IBM-1047 -t ISO8859-1 .gae '
    cmd = cmd '> .gitattributes '
    cmd = cmd '&& rm .gae'
    x = bpxwunix(cmd,,so.,se.)
      /* make sure the file is tagged as binary */
    if type = 1 then
    tagcmd = 'chtag -bR' convdir'/'fileo'/'ndfile
    if type = 3 then
    tagcmd = 'chtag -bR' convdir'/'fileo
    x = bpxwunix(tagcmd,,so.,se.)
    call get_binfiles
  end
  return

get_binfiles:
  /* ---------------------------------------------------------\
  | Name:      get_binfiles                                    |
  |                                                            |
  | Function:  Fills the global binfiles. stem with all        |
  |            current repo files that are added as binary.    |
  \---------------------------------------------------------- */
  cmd = 'cd 'localrep'/'zigirep' &&'
  cmd = cmd 'cat .gitattributes | grep BINARY'
  cmd = cmd '| cut -d" " -f1'
  x = bpxwunix(cmd,,so.,se.)
  binfiles. = null
  do b = 1 to so.0
    binfiles.b = so.b
  end
  binfiles.0 = so.0
  return 0

bin_already: Procedure expose binfiles.
  arg file
  if datatype(binfiles.0) /= 'NUM' then call get_binfiles
  do b = 1 to binfiles.0
    if file = binfiles.b then return 1
    if pos('/',binfiles.b) = 0
    then test = binfiles.b
    else parse value binfiles.b with test'/'.
    if file = test then return 1
  end
  return 0
