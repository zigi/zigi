  /* --------------------  rexx procedure  -------------------- *
  | Name:      zigirefd                                        |
  |                                                            |
  | Function:  ZIGI code to check/update the last reference    |
  |            date for a z/OS dataset. Used to determine if   |
  |            the dataset needs to be checked for updates.    |
  |                                                            |
  | Syntax:    check = zigirefd(zigidir pdshlq qualignr option)|
  |                                                            |
  |            parameters:                                     |
  |            zigidir - OMVS directory for the repository     |
  |            pdshlq  - hlq for z/OS datasets                 |
  |            qualignr- # of hlq quals to ignore              |
  |            option  - C to check                            |
  |                      U to update                           |
  |                      D to delete                           |
  |                                                            |
  |            check will be a string of datasets that have    |
  |            changed since the last ZIGI access.             |
  |                                                            |
  |            A value of * will indicate to check ALL files.  |
  |                                                            |
  | Notes:     A file will be created in the users home dir    |
  |            under a .zigirefd directory with the dataset    |
  |            name and last access information.               |
  |                                                            |
  | Author:    Lionel B. Dyck                                  |
  |                                                            |
  | History:  (most recent on top)                             |
  |            08/25/20 LBD - Fix temp dsn if no tso prefix    |
  |            07/14/20 LBD - Change .zigi to .zigirefd        |
  |            06/10/20 LBD - Use SHAREAS=YES                  |
  |            05/08/20 HBK - Fix bug if dir is not in home    |
  |                     LBD - Add D (delete) option            |
  |            03/18/20 LBD - Refinement                       |
  |            03/17/20 LBD - Creation                         |
  | ---------------------------------------------------------- |
  |    ZIGI - the z/OS ISPF Git Interface                      |
  |                                                            |
  |    This program is free software: you can redistribute it  |
  |    and/or modify it under the terms of the GNU General     |
  |    Public License as published by the Free Software        |
  |    Foundation, either version 3 of the License, or (at     |
  |    your option) any later version.                         |
  |                                                            |
  |    This program is distributed in the hope that it will be |
  |    useful, but WITHOUT ANY WARRANTY; without even the      |
  |    implied warranty of MERCHANTABILITY or FITNESS FOR A    |
  |    PARTICULAR PURPOSE.  See the GNU General Public License |
  |    for more details.                                       |
  |                                                            |
  |    You should have received a copy of the GNU General      |
  |    Public License along with this program.  If not, see    |
  |    <https://www.gnu.org/licenses/>.                        |
  * ---------------------------------------------------------- */

  parse arg dir hlq qualignr opt

  parse value '' with null check outfiles. last_check
  outc = 0

  parse value date('j') with 1 yy 3 ddd
  today = '20'yy''ddd

  if left(dir,1) = "'" then dir = strip(dir,'B',"'")
  if left(hlq,1) = "'" then hlq = strip(hlq,'B',"'")
  if left(opt,1) = "'" then opt = strip(opt,'B',"'")

  if qualignr > 0 then do
    hlq = translate(hlq,' ',".")
    hlq = subword(hlq,1,qualignr)
    hlq = translate(hlq,'.'," ")
  end

  bpxenv.1 = '_BPX_SHAREAS=YES'
  bpxenv.0 = 1

  /* ---------------------------- *
  | Get the users home directory |
  * ---------------------------- */
  Address syscall
  'getcwd homedir'

  /* ---------------------------------------------------------- *
  | Get the repositories subdirectory by removing the users    |
  | home directory from it and use the entire thing if the pwd |
  | is not part of it.  Remove trailing /.                     |
  * ---------------------------------------------------------- */
  homedir = homedir'/'
  dl = length(homedir)
  if left(dir,dl) = homedir
  then wdir = substr(dir,dl+1)
  else wdir = dir
  if right(wdir,1) = '/' then wdir = strip(wdir,'T','/')

  /* ------------------------------ *
  | Translate the directory / to _ |
  * ------------------------------ */
  refdir = translate(wdir,'_','/')
  ref_file = homedir'.zigirefd/'refdir

  'lstat' homedir'.zigirefd/' stem.
  if stem.0 = 0 then do
    'mkdir' homedir'.zigirefd/ 755'
  end

  Address TSO

  opt = translate(opt)

  Select
  When opt = 'C' then do
    cmd = 'cat' ref_file
    x = bpxwunix(cmd,,so.,se.,bpxenv.)
    if x > 0 then return '*'
    last_check = strip(substr(so.i,2))
    do i = 1 to so.0
      parse value so.i with dsn date .
      files.dsn = date
    end
    call check_files
    if check = null then check = '*'
  end
  When opt = 'D' then do
    cmd = 'rm' ref_file
    x = bpxwunix(cmd,,so.,se.,bpxenv.)
    check = 0
    end
  otherwise call update
  end

  return check

update:
  call check_files
  outfiles.0 = outc
  if outc = 0 then return check
  fdd = 'ZG'time('s')
  if sysvar('syspref') = null
     then pref = userid()'.'
     else pref = sysvar('syspref')'.'
  fdsn = "'"pref""fdd"'"
  'alloc f('fdd') ds('fdsn') new spa(1,1) tr recfm(f b)' ,
    'lrecl(80) blksize(32720)'
  'execio * diskw' fdd '(finis stem outfiles.'
  'free f('fdd')'
  call outtrap 'x.'
  'oput' fdsn "'"ref_file"'" 'text'
  'delete' fdsn
  call outtrap 'off'
  return outfiles.0

Check_files:
  address syscall ,
    'readdir' dir f.
  do i = 1 to f.0
    file = f.i
    if left(file,1) = '.' then iterate
    if dsnvalid("'"hlq"."file"'") /= 'OK' then iterate
    x = listdsi("'"hlq"."file"'")
    if x > 0 then iterate
    file = sysdsname
    parse value sysrefdate with yyyy'/'ddd
    sysrefdate = yyyy''ddd
    if opt = 'C' then
    Select
      When sysrefdate = today then check = check sysdsname
      when datatype(files.file) /= 'NUM' then check = check sysdsname
      When sysrefdate > files.file then check = check sysdsname
      otherwise nop
    end
    else do
      outc = outc + 1
      outfiles.outc = sysdsname sysrefdate
    end
  end
  return

dsnvalid: Procedure Expose null
  /* Tests for a 'potentially valid' z/OS datasetname */
  parse arg val_dsn
  if left(val_dsn,1) = '.' then return 'NO'
  if strip(val_dsn) = null then return 'NO'
  val_dsn = strip(val_dsn,'B',"'")
  upp = TRANSLATE(val_dsn,".ABCDEFGHIJKLMNOPQRSTUVWXYZ",,
    ".abcdefghijklmnopqrstuvwxyz")
  tdsn = translate(val_dsn,' ','.')
  retval = 'OK'   /* asssume good */
  do i = 1 to words(tdsn)
    w = word(tdsn,i)
    if length(w) > 8 then retval = 'NO'
    if pos(left(w,1),'ABCDEFGHIJKLMNOPQRSTUVWXYZ$#@') = 0 then retval = 'NO'
    w = translate(w,left(' ',39,' '), ,
      'ABCDEFGHIJKLMNOPQRSTUVWXYZ-$#@0123456789')
    w = strip(w)
    if w /= null then retval = 'NO'
  end
  if retval /= 'NO' then
  if upp = val_dsn then do
    smsg = msg()
    call msg 'OFF'
    if sysdsn(val_dsn) = "OK" | sysdsn(val_dsn) = "DATASET NOT FOUND"
    then retval = 'OK'
    else retval = "NO"
    call msg smsg
  end
  return retval
