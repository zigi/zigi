 /*---------------------  rexx procedure  -------------------- *
  | Name:      ZIGIEME                                         |
  |                                                            |
  | Function:  ISPF Edit Macro to                              |
  |            - Confirm ALL merge conflict records are gone   |
  |            - Save the current member if changed            |
  |            - End the Edit session                          |
  |                                                            |
  | Author:    Lionel B. Dyck                                  |
  |                                                            |
  | History:  (most recent on top)                             |
  |            12/13/19 LBD - creation                         |
  |                                                            |
  | ---------------------------------------------------------- |
  |    zigi - the z/OS ISPF Git Interface                      |
  |    Copyright (C) 2020 - Henri Kuiper and Lionel Dyck       |
  |                                                            |
  |    This program is free software: you can redistribute it  |
  |    and/or modify it under the terms of the GNU General     |
  |    Public License as published by the Free Software        |
  |    Foundation, either version 3 of the License, or (at     |
  |    your option) any later version.                         |
  |                                                            |
  |    This program is distributed in the hope that it will be |
  |    useful, but WITHOUT ANY WARRANTY; without even the      |
  |    implied warranty of MERCHANTABILITY or FITNESS FOR A    |
  |    PARTICULAR PURPOSE.  See the GNU General Public License |
  |    for more details.                                       |
  |                                                            |
  |    You should have received a copy of the GNU General      |
  |    Public License along with this program.  If not, see    |
  |    <https://www.gnu.org/licenses/>.                        |
  * ---------------------------------------------------------- */
  Address ISREdit
  'Macro (options)'

  /* ---------------------------------- *
  | Get the current member information |
  * ---------------------------------- */
  '(member)  = member'
  '(dataset) = dataset'
  '(changed) = data_changed'
  'Reset all'

  /* --------------------------------------------- *
  | Check for merge conflict records and if found |
  | - insert warning message                      |
  | - prevent save or end                         |
  * --------------------------------------------- */
  "find '<<<<<<<' first 1"
  if rc = 0 then call dontsave
  "find '=======' first 1"
  if rc = 0 then call dontsave
  "find '>>>>>>>' first 1"
  if rc = 0 then call dontsave

  /* ----------------------------- *
  | If the data was changed then: |
  | - save the data               |
  | - update the changed string   |
  | - copy to uss                 |
  * ----------------------------- */
  if changed = 'YES' then do
    'Builtin Save'
  end

  /* ---------------------------------- *
  | Now issue the End command and exit |
  * ---------------------------------- */
  Address ISREdit 'Builtin End'

  exit

  /* ------------------ *
  | Don't Save routine |
  * ------------------ */
  dontsave:
  msg = 'End Cancelled as git merge conflict records found.'
  'line_before 1 = msgline (msg)'
  msg = 'Resolve ALL conflicts and Remove ALL merge conflict records'
  'line_before 1 = msgline (msg)'
  msg = 'before trying to Save or End. Use Cancel if you can not resolve.'
  'line_before 1 = msgline (msg)'
  msg = left('-',70,'-')
  'line_before 1 = msgline (msg)'
  'cursor = 1'
  exit 8
