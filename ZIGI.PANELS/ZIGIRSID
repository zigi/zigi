)Attr
 $ type(input)  caps(on ) hilite(uscore) intens(low)
)Body Window(45,4) cmd()
+
+Enter the userid to be set for all members:
+Userid:$resetid +
+
)Init
 &zwinttl = 'ZIGI Reset ID Prompt'
)Proc
 &resp = .resp
*REXX(* zedsmsg zedlmsg resp csr)
  zedsmsg = ''
  if length(resetid) = 8 then do
    CVT      = C2D(Storage(10,4))
    TVT      = C2D(Storage(D2X(CVT + 156),4))   /* cvt + 9c */
    TSVTUMXL = C2D(Storage(D2X(tvt  + 6),1))  /* tvt + 6 */
    if tsvtumxl < 8 then do
      zedsmsg = 'Error'
      zedlmsg = resetid 'is 8 characters and this system does not' ,
        'support 8 character TSO userids at this time.'
      csr = 'resetid'
    end
  end
*ENDREXX
 if (&zedsmsg NE &Z)
    .MSG = ISRZ001
    .CURSOR = &CSR
 if (&zedsmsg EQ &Z)
  ver (&resetid,nb,dsname)
)end
/* -------------------------------------------------------------------------- */
/* ZIGI - the z/OS ISPF Git Interface                                         */
/* Copyright (C) 2020-2024 GPL V3 - Henri Kuiper, Lionel Dyck, Ray Mullins    */
/* -------------------------------------------------------------------------- */
